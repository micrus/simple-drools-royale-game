package com.sample
 
 /*
 ====== SET DI REGOLE PER GESTIRE IL MOVIMENTO DEL PERSONAGGIO
 */
 
 //Start turn
 rule "Start turn: init character"
 salience 200000
 	when 	
		Turn (status == TurnState.INIT)
 		character: Character ( status == Status.MOVED )
 	then 
 		character.setStatus(Status.NOT_MOVED);
 		update (character);
 end
 
  rule "Start turn: performing"
  salience 200000
 	when 	
		turn: Turn (status == TurnState.INIT)
 		not Character ( status == Status.MOVED )
 	then 
 		turn.setStatus(TurnState.PERFORMING);
 		update (turn);
 end
 
 		
 
 //Può muoversi a destra
 rule "MOVE RIGHT"
    when
	    setting : Settings( gameOver == false )
		action : PlayerAction( action == Moves.GO_RIGHT ) and
		hero : Hero ( row < setting.getDimension() - 1 ) and
		not Wall (col == hero.getCol() && row == hero.getRow() +1 )and
		not NPC (col == hero.getCol() && row == hero.getRow() +1 )
	then
       System.out.println("Move Right!");
       hero.setRow(hero.getRow()+1);
	   hero.setStatus(Status.MOVED);
       retract( action );
       update( hero );
 end
 
  //Non può muoversi perchè ha un muro a destra
  rule "CANNOT MOVE RIGHT 1"
    when
	    setting : Settings( gameOver == false )
		action : PlayerAction( action == Moves.GO_RIGHT ) and
		hero : Hero ( ) and
		(Wall (col == hero.getCol() && row == hero.getRow() +1 ) or
		NPC (col == hero.getCol() && row == hero.getRow() +1 ))
	then
       System.out.println("Cannote move Right!");
       hero.setStatus(Status.MOVED);       
       retract ( action );
       update ( hero );
 end
  
   //Non può muoversi perchè è ai bordi
   rule "CANNOT MOVE RIGHT 2"
    when
	    setting : Settings( gameOver == false )
		action : PlayerAction( action == Moves.GO_RIGHT ) and
		hero : Hero ( row >= setting.getDimension() - 1 )
	then
       System.out.println("Cannot move Right!");
       hero.setStatus(Status.MOVED);
       retract( action );
       update( hero );
       
 end
  

 rule "MOVE DOWN"
    when
	    setting : Settings( gameOver == false )
		action : PlayerAction( action == Moves.GO_DOWN ) and
		hero : Hero ( col < setting.getDimension() - 1 )and 
		not Wall (col == hero.getCol()+1 && row == hero.getRow() )and 
		not NPC (col == hero.getCol()+1 && row == hero.getRow() )
		
    then
       System.out.println("Move down!");
       hero.setCol(hero.getCol()+1);
       hero.setStatus(Status.MOVED);
       retract( action );
       update( hero );
 end
 
 rule "CANNOT MOVE DOWN 1"
    when
	    setting : Settings(gameOver == false )
		action : PlayerAction( action == Moves.GO_DOWN ) and
		hero : Hero ( col >= setting.getDimension() - 1 )
		
    then
       System.out.println("Cannot Move down!");
       hero.setStatus(Status.MOVED);
       retract( action );
       update( hero );
 end
 
  rule "CANNOT MOVE DOWN 2"
    when
	    setting : Settings( gameOver == false )
	    hero : Hero ( )
		action : PlayerAction( action == Moves.GO_DOWN ) and
		(Wall ( col == hero.getCol()+1 && row == hero.getRow() ) or
		NPC (col == hero.getCol()+1 && row == hero.getRow()) )
		
    then
       System.out.println("Cannot Move down!");
       hero.setStatus(Status.MOVED);
       retract( action );
       update( hero );
 end
 
 
  rule "MOVE UP"
    when
    	Settings( gameOver == false )
		action : PlayerAction( action == Moves.GO_UP ) and
		hero : Hero ( col > 0 ) and
		not Wall (col == hero.getCol()-1 && row == hero.getRow())and
		not NPC (col == hero.getCol()-1 && row == hero.getRow())
		
    then
       System.out.println("Move up!");
       hero.setCol(hero.getCol()-1);
       hero.setStatus(Status.MOVED);
       retract( action );
       update( hero );
 end
 
   rule "CANNOT MOVE UP 1"
    when
    	Settings( gameOver == false )
		action : PlayerAction( action == Moves.GO_UP ) and
		hero : Hero ( col <= 0 ) 
		
    then
       System.out.println("Cannot Move up!");
       hero.setStatus(Status.MOVED);
       retract( action );
       update( hero );
 end
 
   rule "CANNOT MOVE UP 2"
    when
    	Settings( gameOver == false )
		action : PlayerAction( action == Moves.GO_UP) and
		hero : Hero ( ) and
	    (Wall (col == hero.getCol()-1 && row == hero.getRow()) or
	    NPC (col == hero.getCol()-1 && row == hero.getRow()))
		
    then
       System.out.println("Cannot Move up!");
       hero.setStatus(Status.MOVED);
       retract( action );
       update( hero );
 end
 
 
   rule "MOVE LEFT"
    when
    	Settings( gameOver == false )
		action : PlayerAction( action == Moves.GO_LEFT ) and
		hero : Hero ( row > 0 ) and 
		not Wall (col == hero.getCol() && row == hero.getRow()-1) and
		not NPC (col == hero.getCol() && row == hero.getRow()-1)
		
    then
       System.out.println("Move left!");
       hero.setRow(hero.getRow()-1);
       hero.setStatus(Status.MOVED);
       retract( action );
       update( hero );
 end
 
   rule "CANNOT MOVE LEFT 1"
    when
    	Settings( gameOver == false )
		action : PlayerAction( action == Moves.GO_LEFT ) and
		hero : Hero ( row <= 0 ) 
		
    then
       System.out.println("Cannot Move left!");
       hero.setStatus(Status.MOVED);
       retract( action );
       update ( hero );
 end
 
    rule "CANNOT MOVE LEFT 2"
    when
    	Settings( gameOver == false )
		action : PlayerAction( action == Moves.GO_LEFT ) and
		hero : Hero (  ) and 
		(Wall (col == hero.getCol() && row == hero.getRow()-1) or
		NPC (col == hero.getCol() && row == hero.getRow()-1))
		
    then
       System.out.println("Cannot Move left!");
       hero.setStatus(Status.MOVED);
       retract( action );
       update( hero );
 end
 
 





 
 
/**
=========SET DI REGOLE PER IL RIMPICCIOLIMENTO DEL CAMPO
*/


 
 // Rule that handle the shrinking of the map
  rule "REMOVE PG OF THE BORDER" salience 10
    when
    	setting : Settings( time!=0 && (time % 10) == 0 && gameOver == false) and
		lom : LocatedOnMap ( ((col in (setting.getLimitColLeft() , setting.getLimitColRight())) || (row in (setting.getLimitRowUp() , setting.getLimitRowDown())))&& removable == 1 )  
    then
		retract (lom);
 end
 
   rule "EFFECTIVELY REMOVE PORTIONS OF MAP" salience 10 
    when
    	setting: Settings( time!= 0 && (time % 10) == 0 && gameOver == false) and
		not LocatedOnMap ( ((col in (setting.getLimitColLeft() , setting.getLimitColRight())) || (row in (setting.getLimitRowUp() , setting.getLimitRowDown()))) && removable == 1 )  
    then
	
		
		for (int i = setting.getLimitColLeft(); i<=setting.getLimitColRight(); i++){
			
			insert ( new Wall(i, setting.getLimitRowUp()) );
			insert ( new Wall(i, setting.getLimitRowDown()) );
			
		}
		
		for (int i = setting.getLimitRowUp() + 1; i<=setting.getLimitRowDown() -1; i++){
			
			insert ( new Wall(setting.getLimitColLeft(), i) );
			insert ( new Wall(setting.getLimitColRight(), i) );
			
		}
		
		setting.decreaseLimit();
		setting.setTime( setting.getTime() + 1);
		update ( setting );
 end
 
 
 // REGOLE PER IL COMBATTIMENTO
 
    rule "ATTACK RIGHT"
    when
    	Settings( gameOver == false )
		action : PlayerAction( action == Moves.ATTACK_RIGHT) and
		hero : Hero () and 
		npc: NPC (col == hero.getCol() && row == hero.getRow()+1)
		
    then
       System.out.println("Attacking enemy!");
       npc.increment(StatAbility.LIFE, -(hero.getStat(StatAbility.ATK)));
       hero.setStatus(Status.MOVED);
	   retract( action );
	   update ( hero );       
       update ( npc );       

 end
 
     rule "ATTACK RIGHT BUT NO ENEMY"
    when
    	Settings( gameOver == false )
		action : PlayerAction( action == Moves.ATTACK_RIGHT ) and
		hero : Hero () and 
		not NPC (col == hero.getCol() && row == hero.getRow()+1)
		
    then
       System.out.println("Everyone fights their inner demons ... but there is no need to attack the wind!");
       hero.setStatus(Status.MOVED);
       retract( action );
       update( hero );

 end
 
   rule "ATTACK LEFT"
    when
    	Settings( gameOver == false )
		action : PlayerAction( action == Moves.ATTACK_LEFT) and
		hero : Hero () and 
		npc: NPC (col == hero.getCol() && row == hero.getRow()-1)
		
    then
       System.out.println("Attacking enemy!");
       npc.increment(StatAbility.LIFE, -(hero.getStat(StatAbility.ATK)));
       hero.setStatus(Status.MOVED);
       update ( npc );  
       update ( hero );     
       retract( action );

 end
 
     rule "ATTACK LEFT BUT NO ENEMY"
    when
    	Settings( gameOver == false )
		action : PlayerAction( action == Moves.ATTACK_LEFT ) and
		hero : Hero () and 
		not NPC (col == hero.getCol() && row == hero.getRow()-1)
		
    then
       System.out.println("Everyone fights their inner demons ... but there is no need to attack the wind!");
       hero.setStatus(Status.MOVED);
       retract( action );
       update ( hero );

 end
 
    rule "ATTACK UP"
    when
    	Settings( gameOver == false )
		action : PlayerAction( action == Moves.ATTACK_UP ) and
		hero : Hero () and 
		npc: NPC (col == hero.getCol()-1 && row == hero.getRow())
		
    then
       System.out.println("Attacking enemy!");
       npc.increment(StatAbility.LIFE, -(hero.getStat(StatAbility.ATK)));
       hero.setStatus(Status.MOVED);
       retract( action );
       update ( npc );  
       update( hero );     

 end
 
     rule "ATTACK UP BUT NO ENEMY"
    when
    	Settings( gameOver == false )
		action : PlayerAction( action == Moves.ATTACK_UP ) and
		hero : Hero () and 
		not NPC (col == hero.getCol()-1 && row == hero.getRow())
		
    then
       System.out.println("Everyone fights their inner demons ... but there is no need to attack the wind!");
       hero.setStatus(Status.MOVED);
       retract( action );
	   update( hero );
 end
 
     rule "ATTACK DOWN"
    when
    	Settings( gameOver == false )
		action : PlayerAction( action == Moves.ATTACK_DOWN ) and
		hero : Hero () and 
		npc: NPC (col == hero.getCol()+1 && row == hero.getRow())
		
    then
       System.out.println("Attacking enemy!");
       npc.increment(StatAbility.LIFE, -(hero.getStat(StatAbility.ATK)));
       hero.setStatus(Status.MOVED);
	   retract( action );
       update ( npc );       
       update( hero );

 end
 
     rule "ATTACK DOWN BUT NO ENEMY"
    when
    	Settings( gameOver == false )
		action : PlayerAction( action == Moves.ATTACK_DOWN) and
		hero : Hero () and 
		not NPC (col == hero.getCol()+1 && row == hero.getRow())
		
    then
       System.out.println("Everyone fights their inner demons ... but there is no need to attack the wind!");
       hero.setStatus(Status.MOVED);
       retract( action );
       update( hero );

 end
 
 rule "REMOVE DIED PG"
 	when 
 		character: Character ( actualStat.getStat(StatAbility.LIFE)<=0 )
 	then 
 		retract (character);
 end
 
 /**
 SET DI REGOLE PER LA VITTORIA O LA SCONFITTA
 */
 
 rule "LOSE GAME BECAUSE YOU DIE"
 	when 
 		setting: Settings( gameOver == false ) and
 		not Hero( )
 	then 
 		setting.setGameOver(true);
 		update (setting);
 end
 
 rule "WIN GAME BECAUSE YOU ARE THE ONLY ONE"
 	when 
 		setting: Settings( gameOver == false ) and
 		Hero( ) and
 		not NPC( )
 	then 
 		setting.setGameOver(true);
 		update (setting);
 end
 
 
 /*
 
 PARTE RELATIVA AL DRAFTING
  
 */
 
 rule "DRAFT"
    when
    	Settings( gameOver == false )
		action : PlayerAction( action == Moves.DRAFT ) and
		hero : Hero () and 
		draft: DraftObject (col == hero.getCol() && row == hero.getRow())
		
    then
       System.out.println("Drafting...");
       hero.increment( draft.getStatToIncrease(), draft.getBonus());
       draft.setAvaiability(draft.getAvaiability() - 1); 
       
       System.out.println("New stats: "+hero.getActualStat().toString() );
       
       hero.setStatus(Status.MOVED);
       retract( action );
       update ( hero );
       update ( draft );       

 end
 
       rule "REMOVE DRAFT"
       salience 10
    when
		
		draft: DraftObject ( avaiability <= 0 )
		
    then
    	
    	retract( draft );

 	end
 
  
rule "DRAFT WITHOUT OBJECT"
    when
    	Settings( gameOver == false )
		action : PlayerAction( action == Moves.DRAFT ) and
		hero : Hero () and 
		not DraftObject (col == hero.getCol() && row == hero.getRow())
		
    then
       System.out.println("Some might say that 'he who seeks finds', but this time you only found mud");
       hero.setStatus(Status.MOVED);
       retract( action );
       update(hero);

 end
 
 
/**
SET DI REGOLE PER I TURNI E LA RIMOZIONE DELLE AZIONI
*/



//Remove each action that have been accomplished

 
 rule "END TURN" 
 	when 
 		setting: Settings( ) and
 		turn : Turn ( status == TurnState.PERFORMING) and
 		not Character ( status == Status.NOT_MOVED )
 	then
 		setting.setTime(setting.getTime() + 1);
 		update (setting);
 		retract(turn);
 end
 
 
 
 
 /*
 
 =========================================[PARTE RELATIVA ALLE MOSSE DEGLI NPC]============================================
 
 */
 
 
 rule "MOVE NPG"
    when
    	Settings( gameOver == false )
		npc : NPC( status == Status.NOT_MOVED)
		
    then
       System.out.println("npg move!");
       npc.setStatus(Status.MOVED);
       update(npc);

 end
 
 
 
 /*
 
 
 
 
 
 Cose da fare sicuramente:
 	
 	Implementare IA
 
 
 */
 
 
