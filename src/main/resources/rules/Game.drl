package com.sample
 
 import com.sample.Logger;

 
 //Start turn
 rule "Start turn: init character"
 salience 200000
 	when 	
		Turn (status == TurnState.INIT)
 		character: Character ( status == Status.MOVED )
 	then 
 		character.setStatus(Status.NOT_MOVED);
 		update (character);
 end
 
  rule "Start turn: performing"
  salience 200000
 	when 	
		turn: Turn (status == TurnState.INIT)
 		not Character ( status == Status.MOVED )
 	then 
 		turn.setStatus(TurnState.PERFORMING);
 		update (turn);
 end
 
 		
 
 // Rule that handle the shrinking of the map
rule "KILL PG OUT OF THE BORDER" salience 300002

    when
    	setting : Settings( time!=0 && (time % 10) == 0 && gameOver == false) 
		character : Character ( ((col in (setting.getLimitColLeft() , setting.getLimitColRight())) || (row in (setting.getLimitRowUp() , setting.getLimitRowDown())))&& removable == 1 )  
    then
    	character.die();
    	update (character);
    	retract(character);
 end

rule "REMOVE DIED PG" 
 salience 300001
 	when 
 		character: Character ( actualStat.getStat(StatAbility.LIFE)<=0 )
 	then 
 		insert (new PlayerSoul(character.getCol(),character.getRow(),1, -character.getStat(StatAbility.LIFE)));
 		insert (new WeaponCraftObject(character.getWeapon(), character.getCol(),character.getRow(),1));
 		retract (character);
 end
 
  rule "REMOVE CRAFT OF THE BORDER" salience 300000
    when
    	setting : Settings( time!=0 && (time % 10) == 0 && gameOver == false) 
		co : CraftObject ( ((col in (setting.getLimitColLeft() , setting.getLimitColRight())) || (row in (setting.getLimitRowUp() , setting.getLimitRowDown())))&& removable == 1 )  
    then
		retract (co);
 end
 
 
rule "EFFECTIVELY REMOVE PORTIONS OF MAP" salience 299999 
    when
    	setting: Settings( time!= 0 && (time % 10) == 0 && gameOver == false)
		not LocatedOnMap ( ((col in (setting.getLimitColLeft() , setting.getLimitColRight())) || (row in (setting.getLimitRowUp() , setting.getLimitRowDown()))) && removable == 1 )  
    then
	
		
		for (int i = setting.getLimitColLeft(); i<=setting.getLimitColRight(); i++){
			
			insert ( new Wall(i, setting.getLimitRowUp()) );
			insert ( new Wall(i, setting.getLimitRowDown()) );
			
		}
		
		for (int i = setting.getLimitRowUp() + 1; i<=setting.getLimitRowDown() -1; i++){
			
			insert ( new Wall(setting.getLimitColLeft(), i) );
			insert ( new Wall(setting.getLimitColRight(), i) );
			
		}
		
		setting.decreaseLimit();
		setting.setTime( setting.getTime() + 1);
		update ( setting );
 end
 
  

 
 
 rule "LOSE GAME BECAUSE YOU DIE"
 salience 200000
 	when 
 		setting: Settings( gameOver == false ) 
 		not Hero( )
 	then 
 		setting.setGameOver(true);
 		Logger.getInstance().println("You lost! What an inglorious end for such a noble warrior");
 		update (setting);
 end
 
 rule "WIN GAME BECAUSE YOU ARE THE ONLY ONE"
 	when 
 		setting: Settings( gameOver == false ) 
 		Hero( ) 
 		not NPC( )
 	then 
 		setting.setGameOver(true);
 		Logger.getInstance().println("Congrats! You win proud warrior!");
 		update (setting);
 end
 
 
 rule "END TURN" 
 	when 
 		setting: Settings( ) 
 		turn : Turn ( status == TurnState.PERFORMING) 
 		not PlayerAction( )
 	then
 		setting.setTime(setting.getTime() + 1);
 		update (setting);
 		retract(turn);
 end
 
 